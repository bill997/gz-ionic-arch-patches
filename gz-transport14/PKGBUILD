# Maintainer: oysstu <oysstu at gmail dot com>
# Contributor: bill <beakless dot bill at gmail dot com>

pkgname=gz-transport14
pkgver=14.0.1
pkgrel=3
_pkgmaj=${pkgver%%.*}
_pkgbase=${pkgname::-${#_pkgmaj}}
pkgdesc="Transport library for component communication based on publication/subscription and service calls."
arch=('any')
url="https://github.com/gazebosim/${_pkgbase}"
license=('Apache-2.0')
depends=(
  'cppzmq'
  'gz-msgs=11'
  'gz-utils=3'
  'protobuf-c'
  'protobuf'
  'zeromq'
)
makedepends=(
  'cmake'
  'doxygen'
  'gz-cmake=4'
  'pybind11'
  'util-linux-libs' # uuid
)
provides=("${_pkgbase}=${_pkgmaj}")
source=("https://github.com/gazebosim/${_pkgbase}/archive/${pkgname}_${pkgver}.tar.gz")
sha256sums=('ba6349c7a226b9669eb94d8a9586381bafa065ba9508263b722bad453fba0f4c'
  '8b5f5c08a4a7669bd25b272f937e291da76996bbf280f7b7b9111f8ddf873c1a')

_build_dir="${_pkgbase}-${pkgname}_${pkgver}/build"

prepare() {
  patch -d "${_pkgbase}-${pkgname}_${pkgver}" -Np1 -i ../../arch-ionic.patch
}

build() {
  mkdir -p "$srcdir/$_build_dir" && cd $_
  cmake .. -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DBUILD_TESTING=OFF \
    -DUSE_SYSTEM_PATHS_FOR_PYTHON_INSTALLATION:BOOL=ON \
    -Wno-dev
  cmake --build .
}

package() {
  DESTDIR="$pkgdir" cmake --install "$srcdir/$_build_dir"
}
